// -*- coding: utf-8; mode: conf-javaprop -*-
// SPDX-FileCopyrightText: (c) 2016 ale5000
// SPDX-License-Identifier: GPL-3.0-or-later
// SPDX-FileType: SOURCE

buildscript {
    apply from: 'dependencies.gradle'
}

plugins {
    id 'base'
    //id 'com.github.hierynomus.license-report' version '0.15.0'
    //id 'com.github.spotbugs' version '4.7.6'
}

develocity.buildScan {
    termsOfUseUrl.set 'https://gradle.com/help/legal-terms-of-use'
    termsOfUseAgree.set 'yes'
    publishing.onlyIf { System.getenv('UPLOAD_BUILD_SCAN') != null }
}

/* ===FUNCTIONS=== */

private void initialize() {
    Properties props = new Properties()

    defaultTasks 'tasks'

    project.buildDir = project.file("output")
    project.file('zip-content/module.prop').withInputStream { props.load(it) }

    description = String.valueOf(props.getProperty('description'))
    version = String.valueOf(props.getProperty('version')).toLowerCase(Locale.ENGLISH).trim()
    ext.ourArchivesBaseName = String.valueOf(props.getProperty('id')).trim()

    //ext.isBeta = version.endsWith('-beta')
    //ext.isAlpha = version.endsWith('-alpha')
    //ext.isSnapshot = version.endsWith('-snapshot')

    println name
    println 'Version: ' + version
    println 'OS: ' + System.properties['os.name']

    if(JavaVersion.current() < JavaVersion.VERSION_17) {
        throw new GradleException('This require Java 17 or later')
    }
}

private String getOurArchivesBaseName()
{
    String val = String.valueOf("${ext.ourArchivesBaseName}")
    if(val.isEmpty() || val == 'null')
        throw new InvalidUserDataException('String is empty.')

    return "${val}"
}

private void configureSigning(def android, File keystorePropsFile)
{
    if(!keystorePropsFile.exists()) return
    if(android == null) throw new GradleException('android is null inside configureSigning()')

    println 'Signed build'

    Properties keystoreProps = new Properties()
    keystoreProps.load(new FileInputStream(keystorePropsFile))

    String keyStorePassword
    boolean fallbackToEnv = keystoreProps.containsKey('fallbackToEnv') && keystoreProps['fallbackToEnv'] == 'true'

    if(keystoreProps.containsKey('keyStorePassword'))
        keyStorePassword = keystoreProps['keyStorePassword']
    else if(fallbackToEnv)
        keyStorePassword = System.getenv('KEYSTORE_PASSWORD')

    if(keyStorePassword == null || keyStorePassword.isEmpty())
        throw new InvalidUserDataException('Keystore password is empty')

    android.signingConfigs {
        config {
            storeFile = rootProject.file(keystoreProps['storeFile'])
            storePassword = keyStorePassword
            keyAlias = keystoreProps['keyAlias']
            keyPassword = keyStorePassword
        }
    }

    android.buildTypes.release.signingConfig android.signingConfigs.config
}

initialize()

/* ===BLOCKS=== */

base {
    archivesName = getOurArchivesBaseName()
}

/* ==TASKS=== */

tasks.register('cleanCache', Delete) {
    delete "${projectDir}/cache"
}

tasks.register('cleanRecoveryOutput', Delete) {
    delete "${projectDir}/recovery-simulator/output"
}

tasks.named('clean').configure {
    finalizedBy tasks.named('cleanRecoveryOutput')
    finalizedBy tasks.named('cleanCache')
}

static String getScriptExt() {
    if(System.properties['os.name'].toLowerCase(Locale.ROOT).contains('windows')) {
        return '.bat'
    } else {
        return '.sh'
    }
}

tasks.register('buildOta', Exec) {
    group = '- OTA build'
    description = 'Build the flashable OTA zip.'

    doFirst {
        println 'Building the flashable OTA zip with Gradle...'
        environment BUILD_TYPE: 'full'
        environment NO_PAUSE: '1'
        executable "${projectDir}/build" + getScriptExt()
    }
}

tasks.register('buildOtaOSS', Exec) {
    group = '- OTA build'
    description = 'Build the flashable OTA zip (open-source components only).'

    doFirst {
        println 'Building the flashable OTA zip (open-source components only) with Gradle...'
        environment BUILD_TYPE: 'oss'
        environment NO_PAUSE: '1'
        executable "${projectDir}/build" + getScriptExt()
    }
}
tasks.named('build').configure {
    finalizedBy buildOtaOSS
}

tasks.register('installTest', Exec) {
    group 'verification'
    description 'Emulate an Android recovery on your PC and inside it execute the flashable zip to see the result.'
    mustRunAfter build, buildOta, buildOtaOSS

    doFirst {
        environment LIVE_SETUP_ALLOWED: 'false' /* Live setup doesn't work when executed through Gradle */
        environment BB_GLOBBING: '1'
        executable "${projectDir}/recovery-simulator/recovery" + getScriptExt()
        args "${projectDir}/output/*.zip"
    }
}

tasks.register('test') {
    dependsOn installTest
}

// Dummy task
tasks.register('install')

tasks.named('wrapper').configure {
    println 'Gradle: ' + gradleVersionTarget
    gradleVersion gradleVersionTarget
    //distributionType Wrapper.DistributionType.ALL
    distributionSha256Sum gradleSha256Sum
}
