// -*- coding: utf-8; mode: conf-javaprop -*-
// SPDX-FileCopyrightText: (c) 2016 ale5000
// SPDX-License-Identifier: GPL-3.0-or-later
// SPDX-FileType: SOURCE

buildscript {
    apply from: 'dependencies.gradle'
}

plugins {
    id 'base'
    //id 'com.github.hierynomus.license-report' version '0.15.0'
    //id 'com.github.spotbugs' version '4.7.6'
}

gradleEnterprise.buildScan {
    termsOfServiceUrl 'https://gradle.com/terms-of-service'
    termsOfServiceAgree 'yes'
    publishAlwaysIf System.getenv('UPLOAD_BUILD_SCAN') != null
}

void configure() {
    println name
    // net.ale5000.microg_unofficial_installer.ota

    defaultTasks 'tasks'
    rootProject.buildDir = "${projectDir}/output"

    Properties props = new Properties()
    file("${projectDir}/zip-content/module.prop").withInputStream { props.load(it) }

    String description = props.getProperty('description')
    String version = props.getProperty('version').trim()
    String archivesBaseName = props.getProperty('id')
    //ext.isBeta = version.endsWith('-beta')
    //ext.isAlpha = version.endsWith('-alpha')
    //ext.isSnapshot = version.endsWith('-snapshot')

    println 'Version: ' + version
    println 'OS: ' + System.properties['os.name']

    if(JavaVersion.current() < JavaVersion.VERSION_11) {
        throw new GradleException('This build must be run with Java 11 or later')
    }
}
configure()

tasks.register('cleanCache', Delete) {
    delete "${projectDir}/cache"
}

tasks.register('cleanRecoveryOutput', Delete) {
    delete "${projectDir}/recovery-simulator/output"
}

tasks.named('clean').configure {
    finalizedBy tasks.named('cleanRecoveryOutput')
    finalizedBy tasks.named('cleanCache')
}

static String getScriptExt() {
    if(System.properties['os.name'].toLowerCase(Locale.ROOT).contains('windows')) {
        return '.bat'
    } else {
        return '.sh'
    }
}

tasks.register('buildOta', Exec) {
    group '- OTA build'
    description 'Build the flashable OTA zip.'

    doFirst {
        println 'Building the flashable OTA zip with Gradle...'
        environment OPENSOURCE_ONLY: 'false'
        executable "${projectDir}/build" + getScriptExt()
    }
}

tasks.register('buildOtaOSS', Exec) {
    group '- OTA build'
    description 'Build the flashable OTA zip (open-source components only).'

    doFirst {
        println 'Building the flashable OTA zip (open-source components only) with Gradle...'
        environment OPENSOURCE_ONLY: 'true'
        executable "${projectDir}/build" + getScriptExt()
    }
}
tasks.named('build').configure {
    finalizedBy buildOtaOSS
}

tasks.register('installTest', Exec) {
    group 'verification'
    mustRunAfter build, buildOta, buildOtaOSS

    doFirst {
        environment BB_GLOBBING: '1'
        executable "${projectDir}/recovery-simulator/recovery" + getScriptExt()
        args "${projectDir}/output/*.zip"
    }
}

tasks.register('test') {
    dependsOn installTest
}

// Dummy task
tasks.register('install')

tasks.named('wrapper').configure {
    println 'Gradle: ' + gradleVersionTarget
    gradleVersion gradleVersionTarget
    distributionType Wrapper.DistributionType.ALL
    distributionSha256Sum gradleSha256Sum
}
